[[administration-databases]]
= Database management

[abstract]
--
This chapter explains how to use Cypher to manage Neo4j databases: creating, modifying, deleting, starting, and stopping individual databases within a single server.
--

Neo4j supports the management of multiple databases within the same DBMS.
The metadata for these databases, including the associated security model, is maintained in a special database called the `system` database.
All multi-database administrative commands must be run against the `system` database.
These administrative commands are automatically routed to the `system` database when connected to the DBMS over Bolt.

include::databases-command-syntax.adoc[]

[[administration-databases-show-databases]]
== Listing databases

There are four different commands for listing databases: listing all databases, listing a particular database, listing the default database, and listing the home database.
These commands return the following columns:

include::show-databases-columns.adoc[]

A summary of all available databases can be displayed using the command `SHOW DATABASES`. 


.Query
[source, cypher]
----
SHOW DATABASES
----

.Result
[role="queryresult",options="header,footer",cols="10*<m"]
|===
| +name+ | +aliases+ | +access+ | +address+ | +role+ | +requestedStatus+ | +currentStatus+ | +error+ | +default+ | +home+
| +"movies"+ | +["films","motion pictures"]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+
| +"neo4j"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +true+ | +true+
| +"system"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+
10+d|Rows: 3
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASES
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[NOTE]
====
Note that the results of this command are filtered according to the `ACCESS` privileges of the user.
However, users with `CREATE/DROP/ALTER DATABASE`, `SET DATABASE ACCESS`, or `DATABASE MANAGEMENT` privileges can see all databases regardless of their `ACCESS` privileges.
If a user has not been granted `ACCESS` privilege to any databases, the command can still be executed but will only return the `system` database, which is always visible.



====

In this example, the detailed information for a particular database can be displayed using the command `SHOW DATABASE name YIELD *`. When a `YIELD`
clause is provided, the full set of columns is returned.



.Query
[source, cypher]
----
SHOW DATABASE movies YIELD *
----

.Result
[role="queryresult",options="header,footer",cols="14*<m"]
|===
| +name+ | +aliases+ | +access+ | +databaseID+ | +serverID+ | +address+ | +role+ | +requestedStatus+ | +currentStatus+ | +error+ | +default+ | +home+ | +lastCommittedTxn+ | +replicationLag+
| +"movies"+ | +["films","motion pictures"]+ | +"read-write"+ | +"84ECAE466C86161AC3DBD1BE98E46BD535188F2821B2997FA00C387419960DF8"+ | +"02268f91-9899-4cce-98ff-00f451f099b5"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+ | +3+ | +0+
14+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASE movies YIELD *
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The number of databases can be seen using a `count()` aggregation with `YIELD` and `RETURN`.


.Query
[source, cypher]
----
SHOW DATABASES YIELD * RETURN count(*) as count
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| +count+
| +3+
1+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASES YIELD * RETURN count(*) as count
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The default database can be seen using the command `SHOW DEFAULT DATABASE`.


.Query
[source, cypher]
----
SHOW DEFAULT DATABASE
----

.Result
[role="queryresult",options="header,footer",cols="8*<m"]
|===
| +name+ | +aliases+ | +access+ | +address+ | +role+ | +requestedStatus+ | +currentStatus+ | +error+
| +"neo4j"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+
8+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DEFAULT DATABASE
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The home database for the current user can be seen using the command `SHOW HOME DATABASE`.


.Query
[source, cypher]
----
SHOW HOME DATABASE
----

.Result
[role="queryresult",options="header,footer",cols="8*<m"]
|===
| +name+ | +aliases+ | +access+ | +address+ | +role+ | +requestedStatus+ | +currentStatus+ | +error+
| +"neo4j"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+
8+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW HOME DATABASE
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

It is also possible to filter and sort the results by using `YIELD`, `ORDER BY` and `WHERE`.


.Query
[source, cypher]
----
SHOW DATABASES YIELD name, currentStatus, requestedStatus ORDER BY currentStatus WHERE name CONTAINS 'e'
----

In this example:

* The number of columns returned has been reduced with the `YIELD` clause.
* The order of the returned columns has been changed.
* The results have been filtered to only show database names containing 'e'.
* The results are ordered by the 'currentStatus' column using `ORDER BY`.

It is also possible to use `SKIP` and `LIMIT` to paginate the results.


.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| +name+ | +currentStatus+ | +requestedStatus+
| +"movies"+ | +"online"+ | +"online"+
| +"neo4j"+ | +"online"+ | +"online"+
| +"system"+ | +"online"+ | +"online"+
3+d|Rows: 3
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASES YIELD name, currentStatus, requestedStatus ORDER BY currentStatus WHERE name CONTAINS 'e'
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[NOTE]
====
Note that for failed databases, the `currentStatus` and `requestedStatus` are different.
This often implies an error, but **does not always**.
For example, a database may take a while to transition from `offline` to `online` due to performing recovery.
Or, during normal operation a database's `currentStatus` may be transiently different from its `requestedStatus` due to a necessary automatic process, such as one Neo4j instance copying store files from another.
The possible statuses are `initial`, `online`, `offline`, `store copying` and `unknown`.



====

[role=enterprise-edition]
[[administration-databases-create-database]]
== Creating databases

Databases can be created using `CREATE DATABASE`.


.Query
[source, cypher]
----
CREATE DATABASE customers
----

[role="statsonlyqueryresult"]
0 rows, System updates: 1

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
CREATE DATABASE customers
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[NOTE]
====
Database names are subject to the <<cypher-manual#cypher-naming, standard Cypher restrictions on valid identifiers>>.
The following naming rules apply:


* Database name length must be between 3 and 63 characters.
* The first character must be an ASCII alphabetic character.
* Subsequent characters can be ASCII alphabetic (`mydatabase`), numeric characters (`mydatabase2`), dots (`main.db`), and dashes (enclosed within backticks, e.g., `CREATE DATABASE ++`main-db`++`).
* Names cannot end with dots or dashes.
* Names that begin with an underscore or with the prefix `system` are reserved for internal use.
          


====

When a database has been created, it will show up in the listing provided by the command `SHOW DATABASES`.


.Query
[source, cypher]
----
SHOW DATABASES
----

.Result
[role="queryresult",options="header,footer",cols="10*<m"]
|===
| +name+ | +aliases+ | +access+ | +address+ | +role+ | +requestedStatus+ | +currentStatus+ | +error+ | +default+ | +home+
| +"customers"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+
| +"movies"+ | +["films","motion pictures"]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+
| +"neo4j"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +true+ | +true+
| +"system"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+
10+d|Rows: 4
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASES
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[role=enterprise-edition]
[[administration-databases-create-database-existing]]
=== Handling Existing Databases

This command is optionally idempotent, with the default behavior to fail with an error if the database already exists. Appending `IF NOT EXISTS` to the command ensures that no error is returned and nothing happens should the database already exist. Adding `OR REPLACE` to the command will result in any existing database being deleted and a new one created.


.Query
[source, cypher]
----
CREATE DATABASE customers IF NOT EXISTS
----

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
CREATE DATABASE customers IF NOT EXISTS
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]


.Query
[source, cypher]
----
CREATE OR REPLACE DATABASE customers
----

This is equivalent to running `DROP DATABASE customers IF EXISTS` followed by `CREATE DATABASE customers`.

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
CREATE OR REPLACE DATABASE customers
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[NOTE]
====
The `IF NOT EXISTS` and `OR REPLACE` parts of this command cannot be used together.


====

[role=enterprise-edition]
[[administration-databases-create-database-options]]
=== Options

The create database command can have a map of options, e.g. `OPTIONS { key : 'value'}`


[options="header"]
|===
| Key | Value | Description
| `existingData` | `use` | Controls how the system handles existing data on disk when creating the database.
Currently this is only supported with `existingDataSeedInstance` and must be set to `use` which indicates the existing data files should be used for the new database.
| `existingDataSeedInstance` | instance ID of the cluster node | Defines which instance is used for seeding the data of the created database.
The instance id can be taken from the id column of the `dbms.cluster.overview()` procedure. Can only be used in clusters.
|===


[NOTE]
====
The `existingData` and `existingDataSeedInstance` options cannot be combined with the `OR REPLACE` part of this command.


====

[role=enterprise-edition]
[[administration-databases-alter-database]]
== Altering databases

Databases can be modified using the command `ALTER DATABASE`. For example, a database always has read-write access mode on creation, unless the configuration parameter `dbms.databases.default_to_read_only` is set to `true`.To change it to read-only, you can use the `ALTER DATABASE` command with the sub-clause `SET ACCESS READ ONLY`. Subsequently, the database access mode can be switched back to read-write using the sub-clause `SET ACCESS READ WRITE`. Altering the database access mode is allowed at all times, whether a database is online or offline. 

Database access modes can also be managed using the configuration parameters `dbms.databases.default_to_read_only`, `dbms.databases.read_only`, and `dbms.database.writable`. For details, see <<operations-manual#manage-databases-parameters, Configuration parameters>>. If conflicting modes are set by the `ALTER DATABASE` command and the configuration parameters, i.e. one says read-write and the other read-only, the database will be read-only and prevent write queries.


.Query
[source, cypher]
----
ALTER DATABASE customers SET ACCESS READ ONLY
----

[role="statsonlyqueryresult"]
0 rows, System updates: 1

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
ALTER DATABASE customers SET ACCESS READ ONLY
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The database access mode can be seen in the `access` output column of the command `SHOW DATABASES`.


.Query
[source, cypher]
----
SHOW DATABASES yield name, access
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| +name+ | +access+
| +"customers"+ | +"read-only"+
| +"movies"+ | +"read-write"+
| +"neo4j"+ | +"read-write"+
| +"system"+ | +"read-write"+
2+d|Rows: 4
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASES yield name, access
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

This command is optionally idempotent, with the default behavior to fail with an error if the database does not exist. Appending `IF EXISTS` to the command ensures that no error is returned and nothing happens should the database not exist.


.Query
[source, cypher]
----
ALTER DATABASE nonExisting IF EXISTS SET ACCESS READ WRITE
----

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
ALTER DATABASE nonExisting IF EXISTS SET ACCESS READ WRITE
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[role=enterprise-edition]
[[administration-databases-stop-database]]
== Stopping databases

Databases can be stopped using the command `STOP DATABASE`.


.Query
[source, cypher]
----
STOP DATABASE customers
----

[role="statsonlyqueryresult"]
0 rows, System updates: 1

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
STOP DATABASE customers
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The status of the stopped database can be seen using the command `SHOW DATABASE name`.


.Query
[source, cypher]
----
SHOW DATABASE customers
----

.Result
[role="queryresult",options="header,footer",cols="10*<m"]
|===
| +name+ | +aliases+ | +access+ | +address+ | +role+ | +requestedStatus+ | +currentStatus+ | +error+ | +default+ | +home+
| +"customers"+ | +[]+ | +"read-only"+ | +"localhost:7687"+ | +"standalone"+ | +"offline"+ | +"offline"+ | +""+ | +false+ | +false+
10+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASE customers
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[role=enterprise-edition]
[[administration-databases-start-database]]
== Starting databases

Databases can be started using the command `START DATABASE`.


.Query
[source, cypher]
----
START DATABASE customers
----

[role="statsonlyqueryresult"]
0 rows, System updates: 1

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
START DATABASE customers
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The status of the started database can be seen using the command `SHOW DATABASE name`.


.Query
[source, cypher]
----
SHOW DATABASE customers
----

.Result
[role="queryresult",options="header,footer",cols="10*<m"]
|===
| +name+ | +aliases+ | +access+ | +address+ | +role+ | +requestedStatus+ | +currentStatus+ | +error+ | +default+ | +home+
| +"customers"+ | +[]+ | +"read-only"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+
10+d|Rows: 1
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASE customers
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[role=enterprise-edition]
[[administration-databases-drop-database]]
== Deleting databases

Databases can be deleted using the command `DROP DATABASE`.


.Query
[source, cypher]
----
DROP DATABASE customers
----

[role="statsonlyqueryresult"]
0 rows, System updates: 1

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
DROP DATABASE customers
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

When a database has been deleted, it will no longer show up in the listing provided by the command `SHOW DATABASES`.


.Query
[source, cypher]
----
SHOW DATABASES
----

.Result
[role="queryresult",options="header,footer",cols="10*<m"]
|===
| +name+ | +aliases+ | +access+ | +address+ | +role+ | +requestedStatus+ | +currentStatus+ | +error+ | +default+ | +home+
| +"movies"+ | +["films","motion pictures"]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+
| +"neo4j"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +true+ | +true+
| +"system"+ | +[]+ | +"read-write"+ | +"localhost:7687"+ | +"standalone"+ | +"online"+ | +"online"+ | +""+ | +false+ | +false+
10+d|Rows: 3
|===

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
SHOW DATABASES
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

This command is optionally idempotent, with the default behavior to fail with an error if the database does not exist. Appending `IF EXISTS` to the command ensures that no error is returned and nothing happens should the database not exist. It will always return an error, if there is an existing alias that targets the database. In that case, the alias needs to be dropped before dropping the database.


.Query
[source, cypher]
----
DROP DATABASE customers IF EXISTS
----

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
DROP DATABASE customers IF EXISTS
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The `DROP DATABASE` command will remove a database entirely. However, you can request that a dump of the store files is produced first, and stored in the path configured using the `dbms.directories.dumps.root` setting (by default `<neo4j-home>/data/dumps`). This can be achieved by appending `DUMP DATA` to the command (or `DESTROY DATA` to explicitly request the default behavior). These dumps are equivalent to those produced by `neo4j-admin dump` and can be similarly restored using `neo4j-admin load`.


.Query
[source, cypher]
----
DROP DATABASE customers DUMP DATA
----

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
DROP DATABASE customers DUMP DATA
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

The options `IF EXISTS` and  `DUMP DATA`/ `DESTROY DATA` can also be combined. An example could look like this:


.Query
[source, cypher]
----
DROP DATABASE customers IF EXISTS DUMP DATA
----

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
DROP DATABASE customers IF EXISTS DUMP DATA
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[role=enterprise-edition]
[[administration-wait-nowait]]
== Wait options

Aside from `SHOW DATABASES` and `ALTER DATABASE`, all database management commands accept an optional
`WAIT`/`NOWAIT` clause. The `WAIT`/`NOWAIT` clause allows you to specify a time limit in
which the command must complete and return. The options are:

* `WAIT n SECONDS` - Return once completed or when the specified time limit of `n` seconds is up.
* `WAIT` - Return once completed or when the default time limit of 300 seconds is up.
* `NOWAIT` - Return immediately.

A command using a `WAIT` clause will automatically commit the current transaction when it executes successfully, as the
command needs to run immediately for it to be possible to `WAIT` for it to complete. Any subsequent commands executed will
therefore be performed in a new transaction. This is different to the usual transactional behavior, and for this reason
it is recommended that these commands be run in their own transaction. The default behavior is `NOWAIT`, so if no clause
is specified the transaction will behave normally and the action is performed in the background post-commit.


.Query
[source, cypher]
----
CREATE DATABASE slow WAIT 5 SECONDS
----

.Result
[role="queryresult",options="header,footer",cols="4*<m"]
|===
| +address+ | +state+ | +message+ | +success+
| +"localhost:7687"+ | +"CaughtUp"+ | +"caught up"+ | +true+
4+d|Rows: 1
|===

The `success` column provides an aggregate status of whether or not the command is considered
successful and thus every row will have the same value. The intention of this column is to make it
easy to determine, for example in a script, whether or not the command completed successfully without
timing out.

ifndef::nonhtmloutput[]
[subs="none"]
++++
<formalpara role="cypherconsole">
<title>Try this query live</title>
<para><database><![CDATA[
CREATE DATABASE `movies`
CREATE ALIAS `films` FOR DATABASE `movies`
CREATE ALIAS `motion pictures` FOR DATABASE `movies`

]]></database><command><![CDATA[
CREATE DATABASE slow WAIT 5 SECONDS
]]></command></para></formalpara>
++++
endif::nonhtmloutput[]

[NOTE]
====
A command with a `WAIT` clause may be interrupted whilst it is waiting to complete. In this event
the command will continue to execute in the background and will not be aborted.


====

