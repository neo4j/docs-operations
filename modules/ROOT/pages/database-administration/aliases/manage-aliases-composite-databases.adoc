:description: How to use Cypher to manage database aliases in composite databases in Neo4j.
[role=enterprise-edition not-on-aura]
[[manage-aliases-composite-databases]]
= Managing database aliases in composite databases

Both local and remote database aliases can be created as part of a composite database.
Starting with Neo4j 2025.04, a database alias can also be set as the default database for a composite database.

////
[source, cypher, role=test-setup]
----
CYPHER 25 CREATE DATABASE `sci-fi-books`;
CYPHER 25 CREATE COMPOSITE DATABASE `library`;
CYPHER 25 CREATE ALIAS `library.sci-fi` FOR DATABASE `sci-fi-books`;
CYPHER 25 CREATE ALIAS `library.romance` FOR DATABASE `romance-books` AT 'neo4j+s://location:7687' USER alice PASSWORD 'password';
CYPHER 25 CREATE COMPOSITE DATABASE garden;
CYPHER 25 CREATE DATABASE `perennial-flowers`;
----
////

[NOTE]
====
Starting with Neo4j 2025.06, a database or remote alias can be assigned a default Cypher version.
However, local aliases and aliases in composite databases cannot be assigned a default Cypher version.
Local aliases always get the Cypher version of their target database and aliases in composite databases always get the Cypher version of the composite database.
====

[[manage-aliases-composite-databases-list]]
== List database aliases in composite databases

Available database aliases in composite databases can be seen using `SHOW ALIASES FOR DATABASE`.
The name of the composite database a particular database alias appears in the returned `composite` column.

The required privileges are described in the xref:authentication-authorization/dbms-administration.adoc#access-control-dbms-administration-alias-management[The DBMS ALIAS MANAGEMENT privileges].

.Query
[source, cypher]
----
SHOW ALIASES FOR DATABASE
----

.Result
[role="queryresult"]
----
+--------------------------------------------------------------------------------------------------+
| name              | composite | database        | location | url                       | user    |
+--------------------------------------------------------------------------------------------------+
| "library.romance" | "library" | "romance-books" | "remote" | "neo4j+s://location:7687" | "alice" |
| "library.sci-fi"  | "library" | "sci-fi-books"  | "local"  | NULL                      | NULL    |
+--------------------------------------------------------------------------------------------------+
----

For a description of all the returned columns of this command, and for ways in which the `SHOW ALIASES FOR DATABASE` command can be filtered for aliases, see xref:database-administration/aliases/manage-aliases-standard-databases.adoc#manage-aliases-list[list aliases for standard databases].


[[create-composite-database-alias]]
== Create database aliases in composite databases

Both local and remote database aliases can be part of a xref::database-administration/composite-databases/concepts.adoc[composite database].

The database alias consists of two parts, separated by a dot: the namespace and the alias name.

The namespace must be the name of the composite database.

.Query
[source, cypher]
----
CREATE ALIAS garden.flowers
FOR DATABASE `perennial-flowers`
----

.Query
[source, cypher]
----
CREATE ALIAS garden.trees
FOR DATABASE trees AT 'neo4j+s://location:7687'
USER alice PASSWORD 'password'
----

When a database alias has been created in a composite database, it will show up in the `constituents` column provided by the command `SHOW DATABASES` and in the `SHOW ALIASES FOR DATABASE` command.

.Query
[source, cypher]
----
SHOW DATABASE garden YIELD name, type, constituents
----

.Result
[role="queryresult"]
----
+-------------------------------------------------------------+
| name     | type        | constituents                       |
+-------------------------------------------------------------+
| "garden" | "composite" | ["garden.flowers", "garden.trees"] |
+-------------------------------------------------------------+
----

.Query
[source, cypher]
----
SHOW ALIASES FOR DATABASE
WHERE composite = 'garden'
----

.Result
[role="queryresult"]
----
+-----------------------------------------------------------------------------------------------------+
| name             | composite | database            | location | url                       | user    |
+-----------------------------------------------------------------------------------------------------+
| "garden.flowers" | "garden"  | "perennial-flowers" | "local"  | NULL                      | NULL    |
| "garden.trees"   | "garden"  | "trees"             | "remote" | "neo4j+s://location:7687" | "alice" |
+-----------------------------------------------------------------------------------------------------+
----

Database aliases cannot target a composite database.

.Query
[source, cypher, role=test-fail]
----
CREATE ALIAS yard FOR DATABASE garden
----

.Error message
[source, output, role="noheader"]
----
Failed to create the specified database alias 'yard': Database 'garden' is composite.
----

From 5.26 onwards, the error message also contains the GQLSTATUS code `42NA6` and the status description `error: syntax error or access rule violation - invalid alias target. Aliases are not allowed to target composite databases.`

== Alter local and remote database aliases in composite databases

Local and remote database aliases belonging to a composite database can be altered using the `ALTER ALIAS` command.
This is the same command that is used for altering aliases that are not part of a composite database.

.Query
[source, cypher]
----
ALTER ALIAS garden.flowers SET DATABASE PROPERTIES { perennial: true }
----

.Query
[source, cypher]
----
ALTER ALIAS garden.trees SET DATABASE TARGET updatedTrees AT 'neo4j+s://location:7687' PROPERTIES { treeVersion: 2 }
----

The updated properties can then be used in queries with the link:{neo4j-docs-base-uri}/cypher-manual/current/functions/graph/#functions-graph-propertiesByName[`graph.propertiesByName()` function].

The changes for all database aliases will show up in the `SHOW ALIASES FOR DATABASE` command.

.Query
[source, cypher]
----
SHOW ALIASES FOR DATABASE YIELD *
----

.Result
[role="queryresult"]
----
+-----------------------------------------------------------------------------------------------------------------------------------------------------+
| name              | composite | database            | location | url                       | user    | driver | defaultLanguage | properties        |
+-----------------------------------------------------------------------------------------------------------------------------------------------------+
| "garden.flowers"  | "garden"  | "perennial-flowers" | "local"  | NULL                      | NULL    | NULL   | NULL            | {perennial: TRUE} |
| "garden.trees"    | "garden"  | "updatedtrees"      | "remote" | "neo4j+s://location:7687" | "alice" | {}     | NULL            | {treeversion: 2}  |
| "library.romance" | "library" | "romance-books"     | "remote" | "neo4j+s://location:7687" | "alice" | {}     | NULL            | {}                |
| "library.sci-fi"  | "library" | "sci-fi-books"      | "local"  | NULL                      | NULL    | NULL   | NULL            | {}                |
+-----------------------------------------------------------------------------------------------------------------------------------------------------+
----

[[delete-composite-database-alias]]
== Delete database aliases in composite databases

To delete an alias in a composite database, use the `DROP ALIAS FOR DATABASE` command.
This is the same command that is used for deleting aliases that are not part of a composite database.

.Query
[source, cypher]
----
DROP ALIAS garden.flowers FOR DATABASE
----

When a database alias has been deleted, it will no longer show up in the `SHOW ALIASES FOR DATABASE` command.

.Query
[source, cypher]
----
SHOW ALIASES FOR DATABASE
----

.Result
[role="queryresult"]
----
+--------------------------------------------------------------------------------------------------+
| name              | composite | database        | location | url                       | user    |
+--------------------------------------------------------------------------------------------------+
| "garden.trees"    | "garden"  | "updatedtrees"  | "remote" | "neo4j+s://location:7687" | "alice" |
| "library.romance" | "library" | "romance-books" | "remote" | "neo4j+s://location:7687" | "alice" |
| "library.sci-fi"  | "library" | "sci-fi-books"  | "local"  | NULL                      | NULL    |
+--------------------------------------------------------------------------------------------------+
----

Additionally, deleted aliases will no longer appear in the `constituents` column for the `SHOW DATABASE` command.

.Query
[source, cypher]
----
SHOW DATABASE garden YIELD name, type, constituents
----

.Result
[role="queryresult"]
----
+-------------------------------------------+
| name     | type        | constituents     |
+-------------------------------------------+
| "garden" | "composite" | ["garden.trees"] |
+-------------------------------------------+
----

[[alias-management-escaping]]
== Database alias names that contain dots

Naming database aliases in composite databases follows the same rule as xref:database-administration/aliases/naming-aliases.adoc[naming aliases for standard databases].
However, when it comes to escaping names using backticks, there are some additional things to consider:

Dots in alias names are ambiguous.
They could either be interpreted as part of the name itself, or as the dot that separates a composite namespace from the alias name.

[role=label--new-2025.06]
=== Conflicting names
In versions earlier than Neo4j 2025.06, it is possible to create conflicting aliases, such as the constituent alias `flowers` within the composite database `garden` as well as the non-composite local alias `garden.flowers`.
Both of these are referred to by the same name, `garden.flowers`.

Neo4j 2025.06 and later versions ensure that no such conflicts exist and throw an exception when attempting to create a new alias with the same name as an existing alias.

[.tabbed-example]
=====
[role=include-with-cypher-5 label--new-2025.06]
======
Creating a regular alias with the same name as an existing composite constituent is disallowed:

.Query
[source, cypher]
----
CYPHER 5 CREATE COMPOSITE DATABASE `garden`
CYPHER 5 CREATE ALIAS `garden`.`flowers` FOR DATABASE `northwind-graph`
CYPHER 5 CREATE ALIAS `garden.flowers` FOR DATABASE `northwind-graph`
----

.Error message
[source, output, role="noheader"]
----
Failed to create the specified database alias 'garden.flowers': Database name or alias already exists.
----

======

[role=include-with-cypher-25 label--new-2025.06]
======

Creating a regular alias with the same name as an existing composite constituent is disallowed.
In Cypher 25, the two names are considered the same since the parts are not quoted separately as they would be in Cypher 5.

.Query
[source, cypher]
----
CYPHER 25 CREATE COMPOSITE DATABASE `garden`
CYPHER 25 CREATE ALIAS `garden.flowers` FOR DATABASE `northwind-graph`
CYPHER 25 CREATE ALIAS `garden.flowers` FOR DATABASE `northwind-graph`
----

.Error message
[source, output, role="noheader"]
----
Failed to create the specified database alias 'garden.flowers': Database name or alias already exists.
----
======
=====

[.tabbed-example]
=====
[role=include-with-cypher-5]
======
Creating a composite constituent with the same name as an existing non-composite alias is disallowed.
This example scenario is already prevented on the second line; thus, the constituent on the third line cannot be created.

.Query
[source, cypher]
----
CYPHER 5 CREATE ALIAS `garden.flowers` FOR DATABASE `northwind-graph`
CYPHER 5 CREATE COMPOSITE DATABASE `garden`
CYPHER 5 CREATE ALIAS `garden`.`flowers` FOR DATABASE `northwind-graph`
----

.Error message
[source, output, role="noheader"]
----
Cannot create database 'garden' because another database 'garden.flowers' exists with an ambiguous name.
----

======

[role=include-with-cypher-25 label--new-2025.06]
======

Creating a composite constituent with the same name as an existing non-composite alias is disallowed.
This example scenario is already prevented on the second line; thus, the constituent on the third line cannot be created.
The Cypher 25 syntax makes no distinction between the names to clarify that they are considered equivalent.

.Query
[source, cypher]
----
CYPHER 25 CREATE ALIAS `garden.flowers` FOR DATABASE `northwind-graph`
CYPHER 25 CREATE COMPOSITE DATABASE `garden`
CYPHER 25 CREATE ALIAS `garden.flowers` FOR DATABASE `northwind-graph`
----

.Error message
[source, output, role="noheader"]
----
Cannot create database 'garden' because another database 'garden.flowers' exists with an ambiguous name.
----
======
=====



=== Cypher 25 specific behaviour
==== Accessing an existing alias with dots

Cypher 25 relies on the guarantee that no conflicting names are allowed in Neo4j 2025.06 and later.
The following queries all act on the same alias, regardless of whether that alias is a composite constituent or not.
The special quoting of separate name parts that is necessary in Cypher 5 is not permitted in Cypher 25.

.Parameters
[source, javascript]
----
{
  "name": "my.garden.beautiful.flowers"
}
----
.Query
[source, cypher]
----
CYPHER 25 ALTER ALIAS `my.garden.beautiful.flowers` SET DATABASE PROPERTIES { perennial: true }
CYPHER 25 ALTER ALIAS $name SET DATABASE PROPERTIES { perennial: true }
CYPHER 25 USE `my.garden.beautiful.flowers` RETURN 1
----

==== Creating a new alias with dots

During `CREATE`, Cypher 25 splits the given name on each dot, left to right, and checks if a corresponding composite database exists.
If no composite database is found, Cypher25 falls back to creating a regular non-composite alias.

For example:

.Query
[source, cypher]
----
CYPHER 25 CREATE COMPOSITE DATABASE `my.garden`
CYPHER 25 CREATE ALIAS `my.garden.beautiful.flowers` FOR DATABASE `northwind-graph`
----

The query attempts to create the following aliases in the given order:

. Constituent alias `garden.beautiful.flowers` within composite database `my`.
. Constituent alias `beautiful.flowers` within composite database `my.garden`.
. Constituent alias `flowers` within composite database `my.garden.beautiful`.
. Regular non-composite alias `my.garden.beautiful.flowers`.

.Query
[source, cypher]
----
CYPHER 25 CREATE COMPOSITE DATABASE `my.garden`
CYPHER 25 CREATE ALIAS `my.garden.beautiful.flowers` FOR DATABASE `northwind-graph`
----
When it finds the composite database `my.garden`, it creates the constituent alias `beautiful.flowers` within that composite.



=== Cypher 5 specific behaviour

==== Quoting database alias and composite database names

The composite database name and the database alias name need to be quoted individually.
Backticks may be added regardless of whether the name contains special characters or not, so it is good practice to always backtick both names, e.g.  `++`composite`++.++`alias`++`.
[NOTE]
====
Separating composite database name and alias name with backticks is no longer supported in Cypher 25.
See <<_cypher_25_specific_behaviour, Cypher 25 specific behaviour>> for details.
====

The following example creates a database alias named `my alias with spaces` as a constituent in the composite database named `my-composite-database-with-dashes`:

////
[source, cypher, role=test-setup]
----
CREATE DATABASE `northwind-graph`;
----
////

.Query
[source, cypher]
----
CYPHER 5 CREATE ALIAS `my-composite-database-with-dashes`.`my alias with spaces` FOR DATABASE `northwind-graph`
----

When not quoted individually, a database alias with the full name `my alias with.dots and spaces` gets created instead:

.Query
[source, cypher]
----
CYPHER 5 CREATE ALIAS `my alias with.dots and spaces` FOR DATABASE `northwind-graph`
----

==== Handling multiple dots

//Examples where dots are not separators between composite name and alias name are impossible to test, because the right escaping cannot be inferred automatically.

Database alias names may also include dots.
Though these always need to be quoted in order to avoid ambiguity with the composite database and database alias split character.

.Query
[source, cypher, role=test-skip]
----
CYPHER 5 CREATE ALIAS `my.alias.with.dots` FOR DATABASE `northwind-graph`
----

.Query
[source, cypher, role=test-skip]
----
CYPHER 5 CREATE ALIAS `my.composite.database.with.dots`.`my.other.alias.with.dots` FOR DATABASE `northwind-graph`
----

==== Single dots and local database aliases
[NOTE]
====
As of Neo4j 2025.06, this feature is no longer deprecated.

There is a special case for local database aliases with a single dot without any existing composite database.
If a composite database `some` exists, the query below will create a database alias named `alias` within the composite database `some`.
If no such database exists, however, the same query will instead create a database alias named `some.alias`:

.Query
[source, cypher]
----
CYPHER 5 CREATE ALIAS some.alias FOR DATABASE `northwind-graph`
----

==== Handling parameters

When using parameters, names cannot be quoted.
When the given parameter includes dots, the first dot will be considered the divider for the composite database.

Consider the query with parameter:

.Parameters
[source, javascript]
----
{
  "aliasname": "mySimpleCompositeDatabase.myAlias"
}
----

.Query
[source, cypher]
----
CYPHER 5 CREATE ALIAS $aliasname FOR DATABASE `northwind-graph`
----

If the composite database `mysimplecompositedatabase` exists, then a database alias `myalias` will be created in that composite database.
If no such composite database exists, then a database alias `mysimplecompositedatabase.myalias` will be created.

On the contrary, a database alias `myalias` cannot be created in composite `mycompositedatabase.withdot` using parameters.
Consider the same query but with the following parameter:

.Parameters
[source, javascript]
----
{
  "aliasname": "myCompositeDatabase.withDot.myAlias"
}
----

Since the first dot will be used as a divider, the command will attempt to create the database alias `withdot.myalias` in the composite database `mycompositedatabase`.
If `mycompositedatabase` does not exist, the command will create a database alias with the name `mycompositedatabase.withdot.myalias`, which is not part of any composite database.

In these cases, it is recommended to avoid parameters and explicitly quote the composite database name and alias name separately to avoid ambiguity.

==== Handling parameters

Further special handling with parameters is needed for database aliases and similarly named composite databases.

Consider the setup:

.Query
[source, cypher, role="noheader test-skip"]
----
CYPHER 5 CREATE COMPOSITE DATABASE foo
CYPHER 5 CREATE ALIAS `foo.bar` FOR DATABASE `northwind-graph`
----

The alias `foo.bar` does not belong to the composite database `foo`.

Dropping this alias using parameters fails with an error about a missing alias:

.Parameters
[source, javascript]
----
{
  "aliasname": "foo.bar"
}
----

.Query
[source, cypher, role=test-fail]
----
CYPHER 5 DROP ALIAS $aliasname FOR DATABASE
----

.Error message
[source, output, role="noheader"]
----
Failed to delete the specified database alias 'foo.bar': Database alias does not exist.
----

//From 5.26 onwards, the error message also contains the GQLSTATUS code `50N00` and the status description `error: general processing exception - internal error. Internal exception raised { $msgTitle }: Failed to create the specified database alias 'foo.bar'. Database alias does not exist.`

Had the composite database `foo` not existed, the database alias `foo.bar` would have been dropped.

In these cases, it is recommended to avoid parameters and explicitly quote the composite database name and alias name separately to avoid ambiguity.
