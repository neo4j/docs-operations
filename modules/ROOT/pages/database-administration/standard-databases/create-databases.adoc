:description: how to create, start, and stop databases in Neo4j. How to use `CREATE DATABASE`, `START DATABASE`, `STOP DATABASE` Cypher commands.
:page-aliases: manage-databases/queries.adoc
[role=enterprise-edition not-on-aura]
[[manage-databases-create]]
= Create, start, and stop databases

Neo4j supports the management of multiple databases within the same DBMS.
The metadata for these databases, including the associated security model, is maintained in a special database called the `system` database.
All multi-database administrative commands must be run against the `system` database.
These administrative commands are automatically routed to the `system` database when connected to the DBMS over Bolt.

[NOTE]
====
Administrative commands should not be used during a rolling upgrade.
For more information, see link:{neo4j-docs-base-uri}/upgrade-migration-guide/upgrade/upgrade-4.4/causal-cluster/[Upgrade and Migration Guide -> Upgrade a cluster].
====

[[create-neo4j-database]]
== Create databases

You can create a database using the Cypher command `CREATE DATABASE`.

[NOTE]
====
Database names are subject to the rules specified in xref:database-administration/standard-databases/naming-databases.adoc[Database names] section.
Having dots (`.`) in the database names is not recommended.
This is due to the difficulty of determining if a dot is part of the database name or a delimiter for a database alias in a composite database.
====

`block` is the default format for all newly-created databases as long as they do not have the xref:configuration/configuration-settings.adoc#config_db.format[`db.format`] setting specified. +
If you want to change it, you can set a new value for the xref:configuration/configuration-settings.adoc#config_db.format[`db.format`] configuration in the _neo4j.conf_ file. +
Alternatively, you can set the store format of new databases using the `CREATE DATABASE databasename OPTIONS {storeFormat: 'the-new-format'}` command.
However, if the store is seeded with `seedURI`, `existingDataSeedServer` or `existingDataSeedInstance`, or if the command is being used to mount pre-existing store files already present on the disk, they will use their current store format without any alterations.

See xref:database-internals/store-formats.adoc[Store formats], for more details about available database store formats in Neo4j.

=== Syntax

[options="header", width="100%", cols="1m,5a"]
[.tabbed-example]
=====
[role=include-with-Cypher-5]
======
[options="header", width="100%", cols="1m,5a"]
|===
| Command | Syntax

| CREATE DATABASE
|
[source, syntax, role="noheader"]
----
CREATE DATABASE name [IF NOT EXISTS]
[TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]]
[OPTIONS "{" option: value[, ...] "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

[source, syntax, role="noheader"]
----
CREATE OR REPLACE DATABASE name
[TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]]
[OPTIONS "{" option: value[, ...] "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----
|===

======

[role=include-with-Cypher-25]
======

[options="header", width="100%", cols="1m,5a"]
|===
| Command | Syntax
| CREATE DATABASE
|
[source, syntax, role="noheader"]
----
CREATE DATABASE name [IF NOT EXISTS]
[[SET] TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]]
[OPTIONS "{" option: value[, ...] "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

[source, syntax, role="noheader"]
----
CREATE OR REPLACE DATABASE name
[[SET] TOPOLOGY n PRIMAR{Y\|IES} [m SECONDAR{Y\|IES}]]
[OPTIONS "{" option: value[, ...] "}"]
[WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----
|===

======
=====


[[manage-databases-create-database-options]]
=== Options

The `CREATE DATABASE` command can have a map of options, e.g. `OPTIONS {key: 'value'}`.

[options="header"]
|===

| Key | Value | Description

| `existingData`
| `use`
|
Controls how the system handles existing data on disk when creating the database.
Currently, this is only supported with `existingDataSeedInstance`, `existingDataSeedServer` and `seedURI`, and must be set to `use`, which indicates the existing data files should be used for the new database.


|`existingDataSeedServer`

`existingDataSeedInstance` label:deprecated[deprecated in 5.25]
| ID of the cluster server
|
Defines which server is used for seeding the data of the created database.
The server ID can be found in the `serverId` column after running `SHOW SERVERS`.
Replaced by `existingDataSeedServer`.

| `seedURI`
| URI to a backup or a dump from an existing database.
|
Defines an identical seed from an external source which will be used to seed all servers.

| `seedConfig`
| Comma-separated list of configuration values.
|
For more information see xref::clustering/databases.adoc#cluster-seed-uri[Seed from URI].

| `seedCredentials` label:deprecated[Deprecated in 5.26]
| credentials
|
Defines credentials that need to be passed into certain seed providers.
It is recommended to use the `CloudSeedProvider` seed provider, which does not require this configuration when seeding from cloud storage.
For more information see xref::clustering/databases.adoc#cloud-seed-provider[CloudSeedProvider].

| `txLogEnrichment`
| `FULL` \| `DIFF` \| `OFF`
|
Defines the level of enrichment applied to transaction logs for Change Data Capture (CDC) purposes.

For details about enrichment mode, see link:{neo4j-docs-base-uri}/cdc/current/get-started/self-managed/#set-enrichment-mode/[Change Data Capture Manual -> Enable CDC on self-managed instances -> Set the enrichment mode].

| `storeFormat`
| `aligned` \| `standard` \| `high_limit` \| `block`
|
Defines the store format if the database created is new.
`high_limit` and `standard` formats are deprecated from 5.23.
For more information on store formats, see xref::database-internals/store-formats.adoc[Store formats].

If the store is seeded with `seedURI`, `existingDataSeedInstance` or `existingDataSeedServer`, or if the command is used to mount pre-existing store files already present on the disk, they will retain their current store format without any modifications.
|===

[NOTE]
====
Starting from Neo4j 2025.01, you can use `existingData`, `seedURI`, `seedConfig`, and `seedCredentials` options together with the `CREATE OR REPLACE DATABASE` command.

The `existingDataSeedInstance` and `existingDataSeedServer` are still not supported with the `CREATE OR REPLACE DATABASE` command.
More details about seeding options can be found in xref::clustering/databases.adoc#cluster-seed[Seed a cluster].
====


=== Examples

==== Create a database

To create a database named `customers`, use the command `CREATE DATABASE` followed by the name of this database.

[source, cypher]
----
CREATE DATABASE customers
----

When you create a database, it shows up in the listing provided by the command `SHOW DATABASES`:

[source, cypher]
----
SHOW DATABASES YIELD name
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
----
+-------------+
| name        |
+-------------+
| "customers" |
| "movies"    |
| "neo4j"     |
| "system"    |
+-------------+
----


==== Create a database with xref:database-administration/standard-databases/wait-options.adoc[`WAIT`]

Sub-clause `WAIT` allows you to specify a time limit in which the command must complete and return.

[source, cypher]
----
CREATE DATABASE slow WAIT 5 SECONDS
----

.Result
[role="queryresult"]
----
+-------------------------------------------------------+
| address          | state      | message     | success |
+-------------------------------------------------------+
| "localhost:7687" | "CaughtUp" | "caught up" | TRUE    |
+-------------------------------------------------------+
----

The `success` column provides an aggregate status of whether or not the command is considered successful.
Thus, every row has the same value, determined on a successful completion without a timeout.


==== Create databases with `IF NOT EXISTS` or `OR REPLACE`

The `CREATE DATABASE` command is optionally idempotent, with the default behavior to fail with an error if the database already exists.
There are two ways to circumvent this behavior.

First, appending `IF NOT EXISTS` to the command ensures that no error is returned and that nothing happens if the database already exists.

[source, cypher]
----
CREATE DATABASE customers IF NOT EXISTS
----

Second, adding `OR REPLACE` to the command deletes any existing database and creates a new one.

[source, cypher]
----
CREATE OR REPLACE DATABASE customers
----

This is equivalent to running `DROP DATABASE customers IF EXISTS` followed by `CREATE DATABASE customers`.

Keep in mind that using `CREATE OR REPLACE DATABASE` also removes indexes and constraints.
To preserve them, run the following Cypher commands before the `CREATE OR REPLACE DATABASE` and save their outputs:

[source, cypher]
----
SHOW CONSTRAINTS YIELD createStatement AS statement
----

[source, cypher]
----
SHOW INDEXES YIELD createStatement, owningConstraint
WHERE owningConstraint IS NULL
RETURN createStatement AS statement
----

The behavior of `IF NOT EXISTS` and `OR REPLACE` apply to both standard and composite databases (e.g. a composite database may replace a standard database or another composite database).

[NOTE]
====
The `IF NOT EXISTS` and `OR REPLACE` parts of these commands cannot be used together.
====


[[manage-databases-start]]
== Start databases

Databases can be started using the command `START DATABASE`.

[NOTE]
====
Both standard databases and composite databases can be started using this command.
====


=== Syntax

[options="header", width="100%", cols="1m,5a"]
|===
| Command | Syntax

| START DATABASE
|
[source, syntax, role="noheader"]
----
START DATABASE name [WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

|===

=== Examples

==== Start a database

Starting a database is a straightforward operation.
Suppose you have a database named `customers`.
To start it, use the following command:

[source, cypher]
----
START DATABASE customers
----

You can see the status of the started database by running the command `SHOW DATABASE name`.

[source, cypher]
----
SHOW DATABASE customers YIELD name, requestedStatus, currentStatus
----

.Result
[role="queryresult"]
----
+-----------------------------------------------+
| name        | requestedStatus | currentStatus |
+-----------------------------------------------+
| "customers" | "online"        | "online"      |
+-----------------------------------------------+
----


==== Start a database with xref:database-administration/standard-databases/wait-options.adoc[`WAIT`]

You can start your database using `WAIT` sub-clause to ensure that the command waits for a specified amount of time until the database is started.

[source, cypher]
----
START DATABASE customers WAIT 5 SECONDS
----


[[manage-databases-stop]]
== Stop databases

Databases can be stopped using the command `STOP DATABASE`.

=== Syntax

[options="header", width="100%", cols="1m,5a"]
|===
| Command | Syntax

| STOP DATABASE
|
[source, syntax, role="noheader"]
----
STOP DATABASE name [WAIT [n [SEC[OND[S]]]]\|NOWAIT]
----

|===

=== Examples

==== Stop a database

To stop a database, use the following command:

[source, cypher]
----
STOP DATABASE customers
----

[NOTE]
====
Both standard databases and composite databases can be stopped using this command.
====

The status of the stopped database can be seen using the command `SHOW DATABASE name`:

[source, cypher]
----
SHOW DATABASE customers YIELD name, requestedStatus, currentStatus
----

.Result
[role="queryresult"]
----
+-----------------------------------------------+
| name        | requestedStatus | currentStatus |
+-----------------------------------------------+
| "customers" | "offline"       | "offline"     |
+-----------------------------------------------+
----

==== Stop a database with xref:database-administration/standard-databases/wait-options.adoc[`WAIT`]

You can also stop your database using the `WAIT` sub-clause, which allows you to specify the amount of time that the system should wait for the database to stop.

[source, cypher]
----
STOP DATABASE customers WAIT 10 SECONDS
----


[NOTE]
====
Databases that are stopped with the `STOP` command are completely shut down and may be started again through the `START` command.
In a cluster, as long as a database is in a shutdown state, it can not be considered available to other members of the cluster.
It is not possible to do online backups against shutdown databases and they need to be taken into special consideration during disaster recovery, as they do not have a running Raft machine while shutdown.
Unlike stopped databases, dropped databases are completely removed and are not intended to be used again at all.
====
