[role=enterprise-edition]
[[tutorial-fabric]]
= Set up and use Fabric
:description: This tutorial walks through the basics of setting up and using Neo4j Fabric. 

Neo4j Fabric is a tool for storing and retrieving data in multiple databases, located in one or many Neo4j DBMS(s), with a single Cypher query.

In this tutorial, you will learn how to:

* xref:tutorial/fabric-tutorial.adoc#tutorial-fabric-model-data[Model your data for Fabric]
* xref:tutorial/fabric-tutorial.adoc#tutorial-fabric-config[Configure Fabric with three databases]
* xref:tutorial/fabric-tutorial.adoc#tutorial-fabric-import[Import data in your databases]
* xref:tutorial/fabric-tutorial.adoc#tutorial-fabric-get-results[Retrieve data with a single Cypher query]

[TIP]
====
For more information on how to manage multiple active databases in Neo4j, see xref:manage-databases/index.adoc[Manage databases]. +
For more details on Fabric, see xref:fabric/index.adoc[Fabric].
====

[[tutorial-fabric-model-data]]
== Model your data for Fabric


[[tutorial-fabric-dataset]]
=== Northwind data

image:fabric/northwind-logo.jpeg[width=100] The example data in this tutorial is based on the Northwind dataset, created by Microsoft.

It contains the sales data of a fictitious small company called “Northwind Traders”.
The data includes customers, products, customer orders, warehouse stock, shipping, suppliers, employees, and sales territories.


[[tutorial-fabric-data-model]]
=== The model

The Northwind graph model consists of the following data:

* Node labels
** `:Product`
** `:Category`
** `:Supplier`
** `:Order`
** `:Customer`

*  Relationship types
** `:SUPPLIES`
** `:PART_OF`
** `:ORDERS`
** `:PURCHASED`

image::fabric/northwind-datamodel.png[title="The Northwind data model", width=350]


[[tutorial-fabric-new-data-model]]
=== Remodeling the Northwind dataset

In this scenario, you imagine that data privacy constraints require customers’ data to be stored in their original region.
For simplicity, there are two regions: the Americas (AME) and Europe (EU).
The first step is to remodel the Northwind dataset, so that customer data can be separated from the Product catalog, which has no privacy constraints.
You create two graphs: one for the Product catalog, which includes `:Product`, `:Category`, `:Supplier`, `:PART_OF`, `:SUPPLIES`, and one partitioned graph in two databases for the Customer orders in EU and AME, with `:Product`, `:Order`, `:Customer`, `:PURCHASED`, and `:ORDERS`.

image::fabric/northwind-fabric-datamodel.png[title="The new data model", width=800, role=middle]

*Data Federation*

This way, the Product and Customer data are in two *disjointed graphs*, with different labels and relationship types.
This is called _Data Federation_.
To query across them, you have to federate the graphs, because relationships cannot span across them.
This is done by using a _proxy node_ modeling pattern: nodes with the `:Product` label must be present in both federated domains.
In the Product catalog graph, nodes with the `:Product` label contain all the data related to a product, while in the Customer graphs, the same label is associated to a proxy node, which only contains `productID`.
The `productID` property allows you to link data across the graphs in this federation.

image::fabric/federation.png[title="Data Federation", width=350, role=middle]

*Data Sharding*

Since the Customer data is for two regions (EU and AME), you have to partition it into two databases.
The resulting two graphs have the same model (same labels, same relationship types), but different data.
This is called _Data Sharding_.

image::fabric/sharding2.png[title="Data Sharding", width=450, role=middle]

In general, there are a couple of main use cases that require sharding.
The most common is scalability, i.e., different shards can be deployed on different servers, splitting the load on different resources.
Another reason could be data regulations: different shards can be deployed on servers, residing in different locations, and managed independently.

[[tutorial-fabric-config]]
== Configure Fabric with three databases

Now that you have a new multi-database model defined, you can start to configure the Fabric infrastructure.

[NOTE]
====
This tutorial uses the Linux or macOS tarball installation.
It assumes that your current work directory is the _<neo4j-home>_ directory of the tarball installation.
====

[[tutorial-fabric-create-databases]]
=== Create three databases

You need three databases: `db0` for the Product catalog, `db1` for the EU customer data, and `db2` for the AME customers.

. Start the Neo4j DBMS.
+
[source, shell, role=noplay]
----
bin/neo4j start
----

. Check all available databases.
+
[source, shell, role=noplay]
----
ls -al data/databases/
----
+
[queryresult]
----
total 0
drwxr-xr-x@  5 username  staff   160 20 May 14:52 .
drwxr-xr-x@  4 username  staff   128 20 May 07:19 ..
drwxr-xr-x  37 username  staff  1184 20 May 14:52 neo4j
-rw-r--r--   1 username  staff     0 20 May 14:37 store_lock
drwxr-xr-x  38 username  staff  1216 20 May 14:52 system
----

. Connect to the Neo4j DBMS using `cypher-shell` with the default credentials and change the password when prompted.
For more information about the Cypher Shell command-line interface (CLI) and how to use it, see xref:tools/cypher-shell.adoc[Cypher Shell].
+
[source, shell, role=noplay]
----
bin/cypher-shell -u neo4j -p neo4j
----
+
[queryresult]
----
Password change required
new password: *****
Connected to Neo4j 4.0.x at neo4j://localhost:7687 as user neo4j.
Type :help for a list of available commands or :exit to exit the shell.
Note that Cypher queries must end with a semicolon.
----

. Change the active database to `system`, to see all available databases.
+
[source, cypher, role=noplay]
----
:use system
----

. Run the command `SHOW DATABASES` to list all available databases.
+
[source, cypher, role=noplay]
----
SHOW DATABASES;
----
+
[queryresult]
----
+------------------------------------------------------------------------------------------------+
| name     | address          | role         | requestedStatus | currentStatus | error | default |
+------------------------------------------------------------------------------------------------+
| "neo4j"  | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | TRUE    |
| "system" | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
+------------------------------------------------------------------------------------------------+

2 rows available after 79 ms, consumed after another 6 ms
----

. Run the command `CREATE DATABASE <database-name>` to create the databases.
+
[source, cypher, role=noplay]
----
CREATE DATABASE db0;
----
+
[queryresult]
----
0 rows available after 93 ms, consumed after another 0 ms
----
+
[source, cypher, role=noplay]
----
CREATE DATABASE db1;
----
+
[queryresult]
----
0 rows available after 12 ms, consumed after another 0 ms
----
+
[source, cypher, role=noplay]
----
CREATE DATABASE db2;
----
+
[queryresult]
----
0 rows available after 7 ms, consumed after another 0 ms
----

. Again run the command `SHOW DATABASES` to verify that the new databases have been created.
+
[source, cypher, role=noplay]
----
SHOW DATABASES;
----
+
[queryresult]
----
+------------------------------------------------------------------------------------------------+
| name     | address          | role         | requestedStatus | currentStatus | error | default |
+------------------------------------------------------------------------------------------------+
| "db0"    | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
| "db1"    | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
| "db2"    | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
| "neo4j"  | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | TRUE    |
| "system" | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
+------------------------------------------------------------------------------------------------+

5 rows available after 10 ms, consumed after another 3 ms
----

. Exit the Cypher Shell command-line tool.
+
[source, cypher, role=noplay]
----
:exit
----

[[tutorial-fabric-fabric-config]]
=== Configure Fabric

You set up the Fabric by configuring all involved databases in the _neo4j.conf_ file.
In this example, the Fabric database is called `fabricnw`.

. Navigate to the _<neo4j-home>/conf/_ folder and open the _neo4j.conf_ file.

. Add the following lines and save it.
+
----
#********************************************************************
# Fabric tutorial
#********************************************************************

fabric.database.name=fabricnw
fabric.graph.0.uri=neo4j://localhost:7687
fabric.graph.0.database=db0
fabric.graph.0.name=product
fabric.graph.1.uri=neo4j://localhost:7687
fabric.graph.1.database=db1
fabric.graph.1.name=customerEU
fabric.graph.2.uri=neo4j://localhost:7687
fabric.graph.2.database=db2
fabric.graph.2.name=customerAME
----

. Navigate back to the _<neo4j-home>_ folder and restart the Neo4j DBMS.
+
[source, shell, role=noplay]
----
bin/neo4j restart
----

. Connect to the Neo4j DBMS using `cypher-shell` and your credentials.
+
[source, shell, role=noplay]
----
bin/cypher-shell -u neo4j -p your-password
----

. Change the active database to `system`.
+
[source, cypher, role=noplay]
----
:use system
----

. Run the command `SHOW DATABASES` to verify that the Fabric database has been configured and is `online`.
+
[source, cypher, role=noplay]
----
SHOW DATABASES;
----
+
[queryresult]
----
+--------------------------------------------------------------------------------------------------+
| name       | address          | role         | requestedStatus | currentStatus | error | default |
+--------------------------------------------------------------------------------------------------+
| "db0"      | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
| "db1"      | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
| "db2"      | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
| "fabricnw" | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
| "neo4j"    | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | TRUE    |
| "system"   | "localhost:7687" | "standalone" | "online"        | "online"      | ""    | FALSE   |
+--------------------------------------------------------------------------------------------------+

6 rows available after 164 ms, consumed after another 17 ms
----

[[tutorial-fabric-import]]
== Import data in your databases

You can use the command `LOAD CSV WITH HEADERS FROM` to import data in the databases.

[[tutorial-fabric-import-product]]
=== Load the Product catalog in db0

. Run the following Cypher query to change the active database to `db0`, and add the product data.
+
[source, cypher, role=noplay]
----
:use db0;

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/products.csv" AS row
CREATE (n:Product)
SET n = row,
n.unitPrice = toFloat(row.unitPrice),
n.unitsInStock = toInteger(row.unitsInStock), n.unitsOnOrder = toInteger(row.unitsOnOrder),
n.reorderLevel = toInteger(row.reorderLevel), n.discontinued = (row.discontinued <> "0");

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/categories.csv" AS row
CREATE (n:Category)
SET n = row;

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/suppliers.csv" AS row
CREATE (n:Supplier)
SET n = row;

CREATE INDEX FOR (p:Product) ON (p.productID);
CREATE INDEX FOR (c:Category) ON (c.categoryID);
CREATE INDEX FOR (s:Supplier) ON (s.supplierID);

MATCH (p:Product),(c:Category)
WHERE p.categoryID = c.categoryID
CREATE (p)-[:PART_OF]->(c);

MATCH (p:Product),(s:Supplier)
WHERE p.supplierID = s.supplierID
CREATE (s)-[:SUPPLIES]->(p);
----

. Verify that the product data is loaded in `db0`.
+
[source, cypher, role=noplay]
----
MATCH (s:Supplier)-[:SUPPLIES]->(p:Product)-[:PART_OF]->(c:Category)
RETURN s.companyName AS Supplier, p.productName AS Product, c.categoryName AS Category
LIMIT 5;
----
+
[queryresult]
----
+----------------------------------------------------------------------------+
| Supplier                            | Product                | Category    |
+----------------------------------------------------------------------------+
| "Plutzer Lebensmittelgroßmärkte AG" | "Rhönbräu Klosterbier" | "Beverages" |
| "Aux joyeux ecclésiastiques"        | "Côte de Blaye"        | "Beverages" |
| "Leka Trading"                      | "Ipoh Coffee"          | "Beverages" |
| "Aux joyeux ecclésiastiques"        | "Chartreuse verte"     | "Beverages" |
| "Bigfoot Breweries"                 | "Steeleye Stout"       | "Beverages" |
+----------------------------------------------------------------------------+

5 rows available after 47 ms, consumed after another 3 ms
----


[[tutorial-fabric-import-customerEU]]
=== Load EU customers and related orders in db1

. Run the following Cypher query to change the active database to `db1`, and add the EU customers and orders.
+
[source, cypher, role=noplay]
----
:use db1;

:param europe => ['Germany', 'UK', 'Sweden', 'France', 'Spain', 'Switzerland', 'Austria', 'Italy', 'Portugal', 'Ireland', 'Belgium', 'Norway', 'Denmark', 'Finland'];

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/customers.csv" AS row
WITH row
WHERE row.country IN $europe
CREATE (n:Customer)
SET n = row;

CREATE INDEX FOR (c:Customer) ON (c.customerID);

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/orders.csv" AS row
WITH row
MATCH (c:Customer)
WHERE row.customerID = c.customerID
CREATE (o:Order)
SET o = row;

CREATE INDEX FOR (o:Order) ON (o.orderID);

MATCH (c:Customer),(o:Order)
WHERE c.customerID = o.customerID
CREATE (c)-[:PURCHASED]->(o);

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/products.csv" AS row
CREATE (n:Product)
SET n.productID = row.productID;

CREATE INDEX FOR (p:Product) ON (p.productID);

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/order-details.csv" AS row
MATCH (p:Product), (o:Order)
WHERE p.productID = row.productID AND o.orderID = row.orderID
CREATE (o)-[details:ORDERS]->(p)
SET details = row, details.quantity = toInteger(row.quantity);
----

. Verify that the EU Customer orders data is loaded in `db1`.
+
[source, cypher, role=noplay]
----
MATCH (c:Customer)-[:PURCHASED]->(o:Order)-[:ORDERS]->(p:Product)
RETURN c.companyName AS Customer, c.country AS CustomerCountry, o.orderID AS Order, p.productID AS Product
LIMIT 5;
----
+
[queryresult]
----
+-------------------------------------------------------------+
| Customer              | CustomerCountry | Order   | Product |
+-------------------------------------------------------------+
| "Alfreds Futterkiste" | "Germany"       | "11011" | "58"    |
| "Alfreds Futterkiste" | "Germany"       | "11011" | "71"    |
| "Alfreds Futterkiste" | "Germany"       | "10835" | "77"    |
| "Alfreds Futterkiste" | "Germany"       | "10835" | "59"    |
| "Alfreds Futterkiste" | "Germany"       | "10692" | "63"    |
+-------------------------------------------------------------+

5 rows available after 32 ms, consumed after another 2 ms
----


[[tutorial-fabric-import-customerAME]]
=== Load AME customers and related orders in db2

. Run the following Cypher query to change the active database to `db2` and add the AME customers and orders.
+
[source, cypher, role=noplay]
----
:use db2;

:param americas => ['Mexico', 'Canada', 'Argentina', 'Brazil', 'USA', 'Venezuela'];

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/customers.csv" AS row
WITH row
WHERE row.country IN $americas
CREATE (n:Customer)
SET n = row;

CREATE INDEX FOR (c:Customer) ON (c.customerID);

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/orders.csv" AS row
WITH row
MATCH (c:Customer)
WHERE row.customerID = c.customerID
CREATE (o:Order)
SET o = row;

CREATE INDEX FOR (o:Order) ON (o.orderID);

MATCH (c:Customer),(o:Order)
WHERE c.customerID = o.customerID
CREATE (c)-[:PURCHASED]->(o);

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/products.csv" AS row
CREATE (n:Product)
SET n.productID = row.productID;

CREATE INDEX FOR (p:Product) ON (p.productID);

LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/order-details.csv" AS row
MATCH (p:Product), (o:Order)
WHERE p.productID = row.productID AND o.orderID = row.orderID
CREATE (o)-[details:ORDERS]->(p)
SET details = row,
details.quantity = toInteger(row.quantity);
----

. Verify that the AME Customer orders data is loaded in `db2`.
+
[source, cypher, role=noplay]
----
MATCH (c:Customer)-[:PURCHASED]->(o:Order)-[:ORDERS]->(p:Product)
RETURN c.companyName AS Customer, c.country AS CustomerCountry, o.orderID AS Order, p.productID AS Product
LIMIT 5;
----
+
[queryresult]
----
+----------------------------------------------------------------------------+
| Customer                             | CustomerCountry | Order   | Product |
+----------------------------------------------------------------------------+
| "Ana Trujillo Emparedados y helados" | "Mexico"        | "10625" | "42"    |
| "Ana Trujillo Emparedados y helados" | "Mexico"        | "10625" | "14"    |
| "Ana Trujillo Emparedados y helados" | "Mexico"        | "10625" | "60"    |
| "Ana Trujillo Emparedados y helados" | "Mexico"        | "10926" | "19"    |
| "Ana Trujillo Emparedados y helados" | "Mexico"        | "10926" | "72"    |
+----------------------------------------------------------------------------+

5 rows available after 35 ms, consumed after another 1 ms

----


[[tutorial-fabric-get-results]]
== Retrieve data with a single Cypher query

Using Fabric, you can retrieve data from all your databases with a single Cypher query.

[[tutorial-fabric-query-product]]
=== Query a single database using Fabric

. Change the active database to `fabricnw` by first changing it to `system` and then to `fabricnw`.
+
[source, cypher, role=noplay]
----
:use system
----
+
[source, cypher, role=noplay]
----
:use fabricnw
----

. Use the Cypher clause `USE` to retrieve data only from one database.
+
[source, cypher, role=noplay]
----
USE fabricnw.product
MATCH (p:Product)
RETURN p.productName AS product
LIMIT 5;
----
+
[queryresult]
----
+--------------------------------+
| product                        |
+--------------------------------+
| "Chai"                         |
| "Chang"                        |
| "Aniseed Syrup"                |
| "Chef Anton's Cajun Seasoning" |
| "Chef Anton's Gumbo Mix"       |
+--------------------------------+

5 rows available after 6 ms, consumed after another 21 ms
----
+
[NOTE]
====
This can also be done directly on the `db0` database without going through the Fabric database.
====

[[tutorial-fabric-query-shards]]
=== Query across multiple shards

Use Fabric to query all databases and get customers whose name starts with A. +

[source, cypher, role=noplay]
----
USE fabricnw.customerAME
MATCH (c:Customer)
WHERE c.customerID STARTS WITH 'A'
RETURN c.customerID AS name, c.country AS country
  UNION
USE fabricnw.customerEU
MATCH (c:Customer)
WHERE c.customerID STARTS WITH 'A'
RETURN c.customerID AS name, c.country AS country
LIMIT 5;
----

[queryresult]
----
+---------------------+
| name    | country   |
+---------------------+
| "ANATR" | "Mexico"  |
| "ANTON" | "Mexico"  |
| "ALFKI" | "Germany" |
| "AROUT" | "UK"      |
+---------------------+

4 rows available after 25 ms, consumed after another 56 ms
----

Or, using a more common Fabric idiom:

[source, cypher, role=noplay]
----
UNWIND [1,2]AS gid
CALL {
	USE fabricnw.graph(gid)
	MATCH (c:Customer)
	WHERE c.customerID STARTS WITH 'A'
	RETURN c.customerID AS name, c.country AS country
}
RETURN name, country
LIMIT 5;
----

[queryresult]
----
+---------------------+
| name    | country   |
+---------------------+
| "ANATR" | "Mexico"  |
| "ANTON" | "Mexico"  |
| "ALFKI" | "Germany" |
| "AROUT" | "UK"      |
+---------------------+

4 rows available after 61 ms, consumed after another 8 ms
----

[[tutorial-fabric-query-all-dbs]]
=== Query across federation and shards

Finally, a more complex query that uses all 3 databases: find all customers who have bought discontinued products in the Meat/Poultry category.

[source, cypher, role=noplay]
----
CALL {
USE fabricnw.product
MATCH (p:Product{discontinued:true})-[:PART_OF]->(c:Category{categoryName:'Meat/Poultry'})
              RETURN COLLECT(p.productID) AS pids
}
WITH *, [g IN fabricnw.graphIds() WHERE g<>0] AS gids
UNWIND gids AS gid
CALL {
	USE fabricnw.graph(gid)
	WITH pids
	UNWIND pids as pid
	MATCH (p:Product{productID:pid})<-[:ORDERS]-(:Order)<-[:PURCHASED]-(c:Customer)
	RETURN DISTINCT c.customerID AS customer, c.country AS country
}
RETURN customer, country
LIMIT 5;
----

[queryresult]
----
+---------------------+
| customer | country  |
+---------------------+
| "GODOS"  | "Spain"  |
| "SAVEA"  | "USA"    |
| "BLONP"  | "France" |
| "HANAR"  | "Brazil" |
| "BOTTM"  | "Canada" |
+---------------------+

5 rows available after 128 ms, consumed after another 75 ms
----

[TIP]
====
First, `fabricnw` calls database `db0` to retrieve all discontinued products in the Meat/Poultry category.
Then, using the returned product IDs, it queries both `db1` and `db2` *in parallel* and gets the customers who have purchased these products and their country.
====


[[tutorial-fabric-end]]
== The end

You have just learned how to store and retrieve data from multiple databases using a single Cypher query. +
For more details on the Neo4j Fabric, see xref:fabric/index.adoc[Fabric].
